# Load .env files by default; the project .env lives one directory up
set dotenv-load := true
set dotenv-filename := ".env"

# List available commands
default:
    @{{ just_executable() }} --list

# Build the production docker image
build:
    #!/usr/bin/env bash
    export BUILD_DATE=$(date -u +'%y-%m-%dT%H:%M:%SZ')
    export GITREF=$(git rev-parse --short HEAD)
    docker compose build --pull prod

# Run command in prod container (default: run the streamlit app)
run *args="": build
    docker compose run --rm --service-ports prod {{ args }}

# Exec command in existing prod container
exec *args="bash": build
    docker compose exec prod {{ args }}

# Run hadolint on the Dockerfile
lint:
    docker run --rm -i ghcr.io/hadolint/hadolint:v2.14.0-alpine < Dockerfile

# Stop and remove all project containers
clean:
    #!/bin/bash
    set -eux
    . .env
    docker compose down
    docker container prune --force --filter label=com.docker.compose.project=$COMPOSE_PROJECT_NAME
